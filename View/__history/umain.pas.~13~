unit umain;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.Buttons, Vcl.ExtCtrls, ucadastropadrao,
  System.ImageList, Vcl.ImgList, Vcl.ComCtrls;

type
  TFrmMain = class(TForm)
    PnlFundo: TPanel;
    PnlLateral: TPanel;
    BtnSair: TSpeedButton;
    BtnRelatorios: TSpeedButton;
    BtnVendas: TSpeedButton;
    BtnProdutos: TSpeedButton;
    BtnClientes: TSpeedButton;
    BtnMenu: TSpeedButton;
    PnlBarra: TPanel;
    ImageList: TImageList;
    PageControlMain: TPageControl;
    procedure BtnMenuClick(Sender: TObject);
    procedure BtnSairClick(Sender: TObject);
    procedure PnlLateralMouseEnter(Sender: TObject);
    procedure PnlLateralMouseLeave(Sender: TObject);
    function MouseStillOverPanel: Boolean;
    procedure FormShow(Sender: TObject);
    procedure BtnClientesClick(Sender: TObject);
    procedure BtnProdutosClick(Sender: TObject);
    procedure FormCloseHandler(Sender: TObject; var Action: TCloseAction);
  private
    procedure ExpandirMenu;
    procedure ContrairMenu;
    procedure OpenTab(const AFormClass: TFormClass; const ATabName: string);

  public
    { Public declarations }
  end;

var
  FrmMain: TFrmMain;
  BExpandido: Boolean;

implementation

uses ucadproduto, ucadcliente;

{$R *.dfm}


procedure TFrmMain.BtnMenuClick(Sender: TObject);
begin
  if BExpandido then
    ContrairMenu()
  else
    ExpandirMenu();

  BExpandido := not BExpandido;
end;

procedure TFrmMain.BtnClientesClick(Sender: TObject);
begin
  ContrairMenu();
  OpenTab(TFrmCadCliente, 'Clientes');
end;

procedure TFrmMain.BtnProdutosClick(Sender: TObject);
begin
  ContrairMenu();
  OpenTab(TFrmCadProduto, 'Produtos');
end;

procedure TFrmMain.BtnSairClick(Sender: TObject);
begin
  Close;
end;

procedure TFrmMain.ContrairMenu;
begin
  PnlLateral.Width := 50;
end;

procedure TFrmMain.ExpandirMenu;
begin
  PnlLateral.Width := 137;
end;

procedure TFrmMain.FormCloseHandler(Sender: TObject; var Action: TCloseAction);
var i: Integer;
    Form: TForm;
begin
  // Identificar a aba correspondente ao formulário fechado
  Form := TForm(Sender);
  for i := 0 to PageControlMain.PageCount - 1 do
  begin
    // Verificar se a aba ainda existe e se está associada corretamente ao formulário
    if Assigned(PageControlMain.Pages[i]) and (PageControlMain.Pages[i].Tag = Integer(Form)) then
    begin
      // Fechar a aba
      PageControlMain.Pages[i].Free;
      Break;
    end;
  end;

  // Garantir que o formulário será corretamente liberado
  //Action := caFree;
end;

procedure TFrmMain.FormShow(Sender: TObject);
begin
  ContrairMenu();
end;

function TFrmMain.MouseStillOverPanel: Boolean;
var MousePos: TPoint;
begin
  // Pega a posição atual do mouse
  GetCursorPos(MousePos);
  // Converte para as coordenadas relativas ao formulário
  MousePos := ScreenToClient(MousePos);

  // Verifica se o mouse ainda está sobre o painel lateral
  Result := PtInRect(PnlLateral.BoundsRect, MousePos);
end;

procedure TFrmMain.OpenTab(const AFormClass: TFormClass; const ATabName: string);
var TabSheet: TTabSheet;
    NewForm: TForm;
    i: Integer;
begin
  // Verificar se a aba já existe
  for i := 0 to PageControlMain.PageCount - 1 do
  begin
    if PageControlMain.Pages[i].Caption = ATabName then
    begin
      PageControlMain.ActivePage := PageControlMain.Pages[i]; // Focar na aba já existente
      Exit;
    end;
  end;

  // Criar uma nova aba
  TabSheet := TTabSheet.Create(PageControlMain);
  TabSheet.PageControl := PageControlMain;
  TabSheet.Caption := ATabName;

  // Instanciar o formulário e associá-lo à nova aba
  NewForm := AFormClass.Create(TabSheet);
  NewForm.Parent := TabSheet;
  NewForm.Align := alClient;
  NewForm.BorderStyle := bsNone;
  NewForm.Show;

  // Associar o fechamento do formulário ao fechamento da aba
  TabSheet.Tag := Integer(NewForm); // Usar a propriedade Tag para armazenar o formulário associado
  NewForm.OnClose := FormCloseHandler; // Vincular o evento OnClose ao manipulador

  // Tornar a aba recém-criada ativa
  PageControlMain.ActivePage := TabSheet;
end;

procedure TFrmMain.PnlLateralMouseEnter(Sender: TObject);
begin
  ExpandirMenu();
end;

procedure TFrmMain.PnlLateralMouseLeave(Sender: TObject);
begin
  if not BExpandido and not MouseStillOverPanel then
    ContrairMenu();
end;

end.
